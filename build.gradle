plugins {
  id "com.gradle.plugin-publish" version "0.9.4"
}

ext {
    user = 'xbib'
    name = 'gradle-plugin-jacc'
    pluginDescription = 'Gradle plugin for using Jacc'
    scmUrl = 'https://github.com/' + user + '/' + name
    scmConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    scmDeveloperConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
}

group = 'org.xbib.gradle.plugin'
version = '1.1.3'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  wagon
}

dependencies {
  compile gradleApi()
  compile 'org.xbib:jacc:2.1.0'
  wagon 'org.apache.maven.wagon:wagon-ssh-external:2.10'
}

task javadocJar(type: Jar, dependsOn: classes) {
  from javadoc
  classifier 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allSource
  classifier 'sources'
}

artifacts {
  archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
  signing {
    sign configurations.archives
  }
}

apply from: 'gradle/publish.gradle'
